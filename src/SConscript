# SConscript
# Copyright 2016 Steve Palmer

Import('common_env')

# Ancilliaries

common_env.rnc2rng('emulator_strict')
common_env.rnc2rng('emulator_permissive', ['emulator_permissive', 'emulator_strict'])

common_env.jing('emulator_strict.jing.txt', r'emulator_strict.rng')
common_env.jing('emulator_permissive.jing.txt', r'emulator_permissive.rng')
common_env.jing('atomrc.jing.txt', [r'emulator_strict.rng', r'atomrc.xml'])
common_env.jing('atomrc_full.jing.txt', [r'emulator_strict.rng', r'atomrc_full.xml'])

common_env.Command(r'plot.bmp', r'build_plot_bmp.py',
                   "python3 $SOURCE $TARGET")

# Main Build

common_env['CCFLAGS']   = r'-Wall -g -pipe -std=c++11 -pthread -O0 -DSERIALIZE_TO_DOT=0'
common_env['LINKFLAGS'] = r'-pthread'
common_env.ParseConfig("pkg-config --cflags --libs liblog4cxx glibmm-2.4")

partslib_env = common_env.Clone()
PARTSLIB = partslib_env.StaticLibrary(r'parts', [ r'part.cpp'
                                                , r'device.cpp'
                                                , r'memory.cpp'
                                                , r'ppia.cpp'
                                                , r'cpu.cpp'
                                                , r'terminal_interface.cpp'
                                                ])

emulator_gui_sdl_env = common_env.Clone()
emulator_gui_sdl_env.ParseConfig("pkg-config --cflags --libs sdl2 libxml++-2.6")
emulator_gui_sdl_env.Program('emulator', [ r'monitor_view.cpp'
                                         , r'keyboard_controller.cpp'
                                         , r'terminal.cpp'
                                         , r'config_xml.cpp'
                                         , r'main.cpp'
                                         , PARTSLIB
                                         ])

#emulator_gui_sdl_env.Program('key', 'key.cpp')
